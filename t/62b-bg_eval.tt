
use strict;
use warnings;

$Forks::Super::LazyEval::USE_ZCALAR = 0;

#
# tests (33) for exercising bg_eval. This code should
# not be run by itself, but should be <require>'d by
# the test scripts (62j-bg_eval.t, 62y-bg_eval.t)
# that wish to run them.
# 

my $t0 = Time::HiRes::time();

# untaint needed when Data::Dumper is the serializer
my $untaint = ${^TAINT};

my $x = bg_eval { sleep 3 ; return 42 } { untaint => $untaint };
my $t = Time::HiRes::time();
ok(defined $Forks::Super::LAST_JOB,        ### 3 ###
   "\$Forks::Super::LAST_JOB set");
ok(defined $Forks::Super::LAST_JOB_ID, "\$Forks::Super::LAST_JOB_ID set");
ok(Forks::Super::isValidPid($Forks::Super::LAST_JOB_ID), 
   "\$Forks::Super::LAST_JOB_ID set");
ok($Forks::Super::LAST_JOB->{_is_bg} > 0, 
   "\$Forks::Super::LAST_JOB marked bg");
my $p = waitpid -1, 0, 10.0;
ok($p == -1, "waitpid doesn't catch bg_eval job");
ok($x == 42, "scalar bg_eval");
my $t1 = Time::HiRes::time();
($t,$t0) = ($t1-$t,$t1-$t0);
my $y = $x;
ok($y == 42, "scalar bg_eval");
ok($t0 >= 2.95 && $t <= 7.95,     ### 10 ### was 3.85, obs 3.97,5.11,7.39
   "scalar bg_eval took ${t}s ${t0}s expected ~3s");
$x = 19;
ok($x == 19, "result is not read only");

SKIP: {

  if ($Forks::Super::SysInfo::SLEEP_ALARM_COMPATIBLE <= 0) {
    skip "alarm and sleep are incompatible on this system; "
      . "can't test bg_eval with a timeout", 2;
  }

  $x = bg_eval { sleep 10; return 19 } { timeout => 2, untaint => $untaint };
  $t = Time::HiRes::time();
  ok(!defined($x->_fetch) || $x eq '',
     "scalar bg_eval undef on failure");
  $t = Time::HiRes::time() - $t;
  ok($t <= 4.5,                     ### 13 ### was 3.25, obs 3.41,4.01
     "scalar bg_eval respected timeout, took ${t}s expected ~2s");
}

Forks::Super::Debug::_use_Carp_Always();

### list context ###
#
# result is a tie'd array so let's exercise the array operations
#

$t = Time::HiRes::time();
my @x = bg_eval {
  sleep 2;
  opendir(X, "t");
  my @f = grep { /\.t$/ } readdir(X);
  closedir X;
  return @f;
} {
  'untaint' => $untaint
};
my @tests = @x;
$t = Time::HiRes::time() - $t;
ok(@tests > 10, "list bg_eval");
ok($t >= 1.84, "list bg_eval took ${t}s expected ~2s");

my $n = @x;
my $u = shift @x;
ok($u =~ /\.t$/ && @x == $n - 1, "list bg_eval shift");
$u = pop @x;
ok(@x == $n - 2 && $u =~ /\.t$/, "list bg_eval pop");
unshift @x, "asdf";
ok(@x == $n - 1, "list bg_eval unshift");
push @x, "qwer", "tyuiop";
ok(@x == $n + 1, "list bg_eval push");
splice @x, 3, 3, "pq";
ok(@x == $n - 1 && $x[3] eq "pq", "list bg_eval splice");
$x[3] = "rst";
ok(@x == $n - 1 && $x[3] eq "rst", "list bg_eval store");
ok($x[5] =~ /.t$/, "list bg_eval fetch");
delete $x[4];
ok(!defined $x[4], "list bg_eval delete");
@x = ();
ok(@x == 0, "list bg_eval clear");

### scalar context, return reference ###

$t0 = Time::HiRes::time();
$x = bg_eval {
  sleep 2;
  opendir(X, "t");
  my $i = 0; my %f = map { $_ => ++$i } grep { !/\.t$/ } readdir(X);
  closedir X;
  return \%f;
} {
  'untaint' => $untaint
};
$t = Time::HiRes::time();
my %others = %$x;
my $t2 = Time::HiRes::time();
($t,$t0) = ($t2-$t,$t2-$t0);
ok($t0 >= 1.95 && $t <= 5.04,           ### 25 ### was 5.04 obs 8.54
   "listref bg_eval took ${t0}s ${t}s expected ~2s");
ok(scalar(keys %others) > 0, "listref bg_eval");
$x = [ "a", "v", "rst" ];
ok(@$x == 3, "listref bg_eval overwrite ok");
waitall;

### test variery of %options ###

$x = 20;
my $w = 14;
$t0 = Time::HiRes::time();
$x = bg_eval {
  sleep 5; return 19
} { name => 'bg_eval_job', delay => 3, on_busy => "queue",
    callback => { queue => sub { $w++ }, start => sub { $w+=2 },
		    finish => sub { $w+=5 } },
    untaint => $untaint
};
$t = Time::HiRes::time();
my $j = Forks::Super::Job::get('bg_eval_job');
ok($j eq $Forks::Super::LAST_JOB, "\$Forks::Super::LAST_JOB updated");
ok($j->{state} eq "DEFERRED", "bg_eval with delay");
ok($w == 14 + 1, "bg_eval job queue callback");
Forks::Super::pause(4);
ok($j->{state} eq "ACTIVE", "bg_eval job left queue " . $j->toString());
ok($w == 14 + 1 + 2, "bg_eval start callback");
ok($x == 19, "scalar bg_eval with lots of options");
$t1 = Time::HiRes::time();
($t,$t0) = ($t1-$t,$t1-$t0);
ok($t0 > 7.80 && $t < 13.5,  ### 34 ### was 9.6 obs 9.99,10.35,11.37,13.20
   "bg_eval with delay took ${t}s ${t0}s, expected ~8s");
ok($w == 14 + 1 + 2 + 5, "bg_eval finish callback");

use Carp;$SIG{SEGV} = sub { 
  Carp::cluck "Caught SIGSEGV during cleanup of $0 ...\n" 
};

1;
